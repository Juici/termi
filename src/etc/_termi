#compdef termi

_termi() {
    local curcontext="$curcontext" ret=1

    local context state state_descr line
    local -A opt_args

    local -a common

    common=(
        '(- 1 *)'{-h,--help}'[show help message]'
    )

    _arguments -s -S -C "${common[@]}" \
        '(- 1 *)'{-V,--version}'[show version information]' \
        '1: :_termi_cmds' \
        '*:: :->args'

    case "$state" in
        (args)
            curcontext="${curcontext%:*}-${words[1]}:"
            case "${words[1]}" in
                (query)
                    _arguments -s -S "${common[@]}" \
                        '1: :_termi_query_cmds'
                    ;;
                (help)
                    _termi_cmds
                    ;;
            esac
            ;;
    esac
}

_termi_cmds() {
    local -a commands
    commands=(
        'query:query supported features'
        'help:show help for subcommand'
    )
    _describe -t commands 'command' commands
}

_termi_query_cmds() {
    local -a commands
    commands=(
        'keyboard-enhancement:query support for progressive keyboard enhancement'
        'desktop-notifications:query support for desktop notifications'
    )
    _describe -t commands 'command' commands
}

_termi